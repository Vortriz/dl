{
    description = "Dev environment with Python (via uv)";

    inputs = {
        # set your systems using: https://github.com/nix-systems/nix-systems?tab=readme-ov-file#available-system-flakes
        systems.url = "github:nix-systems/x86_64-linux";

        nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
        flake-utils = {
            url = "github:numtide/flake-utils";
            inputs.systems.follows = "systems";
        };
        devshell = {
            url = "github:numtide/devshell";
            inputs.nixpkgs.follows = "nixpkgs";
        };
        pre-commit-hooks = {
            url = "github:cachix/git-hooks.nix";
            inputs.nixpkgs.follows = "nixpkgs";
        };
        treefmt-nix = {
            url = "github:numtide/treefmt-nix";
            inputs.nixpkgs.follows = "nixpkgs";
        };
        nix-filter.url = "github:numtide/nix-filter";
    };

    outputs =
        {
            self,
            nixpkgs,
            flake-utils,
            devshell,
            pre-commit-hooks,
            treefmt-nix,
            nix-filter,
            ...
        }:
        flake-utils.lib.eachDefaultSystem (
            system:
            let
                inherit (nixpkgs) lib;

                pkgs = import nixpkgs {
                    inherit system;
                    overlays = [ devshell.overlays.default ];
                };

                filter = nix-filter.lib;

                treefmtEval = treefmt-nix.lib.evalModule pkgs {
                    # Used to find the project root
                    projectRootFile = "flake.nix";

                    programs = {
                        # nix
                        deadnix.enable = true;
                        statix.enable = true;
                        nixfmt.enable = true;

                        # python
                        ruff-format.enable = true;
                        ruff-check.enable = true;
                    };

                    settings = {
                        global.excludes = [
                            # Exclude all files generated by nvfetcher
                            "**sources/*"
                        ];

                        formatter = {
                            deadnix.priority = 1;
                            statix.priority = 2;
                            nixfmt = {
                                priority = 3;
                                options = [ "--indent=4" ];
                            };
                            ruff-format.priority = 4;
                            ruff-check = {
                                priority = 5;
                                options = [ "--fix-only" ];
                            };
                        };
                    };
                };
            in
            {
                packages = {
                    dl = pkgs.python3Packages.callPackage ./package.nix { inherit filter; };
                    default = self.packages.${system}.dl;
                };

                formatter = treefmtEval.config.build.wrapper;

                checks = {
                    pre-commit-check = pre-commit-hooks.lib.${system}.run {
                        src = ./.;
                        hooks = {
                            flake-checker = {
                                enable = true;
                                after = [ "treefmt-nix" ];
                            };
                            treefmt = {
                                enable = true;
                                package = self.formatter.${system};
                            };
                        };
                    };
                };

                # Impurely using uv to manage virtual environments
                devShells.default =
                    let
                        python-pkg = pkgs.python313;
                    in
                    pkgs.devshell.mkShell {
                        name = "dl";
                        devshell.motd = "";

                        packages = [
                            python-pkg
                        ]
                        ++ (with pkgs; [
                            uv
                            ruff
                        ]);

                        env = [
                            {
                                # Prevent uv from managing Python downloads
                                name = "UV_PYTHON_DOWNLOADS";
                                value = "never";
                            }
                            {
                                # Force uv to use nixpkgs Python interpreter
                                name = "UV_PYTHON";
                                value = python-pkg.interpreter;
                            }
                            {
                                # Python libraries often load native shared objects using dlopen(3).
                                # Setting LD_LIBRARY_PATH makes the dynamic library loader aware of libraries without using RPATH for lookup.
                                # We use manylinux2014 which is compatible with 3.7.8+, 3.8.4+, 3.9.0+
                                name = "LD_LIBRARY_PATH";
                                prefix = lib.makeLibraryPath pkgs.pythonManylinuxPackages.manylinux2014;
                            }
                        ];

                        devshell.startup.default.text = "unset PYTHONPATH";
                    };
            }
        );
}
